//
// .create command - create items
// Items created with this command will not decay.
// Two forms:
//   ".create [objtype] [amount]" - specify object type and amount
//       amount defaults to 1
//   ".create" - show the item creation menu, create one of that item
//
use uo;

program textcmd_create( who, text )
    if (text)
        local params := SplitWords( text );
        local objtype := params[1];
        local amount := CInt( params[2] );

        if (CInt(objtype))
            objtype := CInt(objtype);
        endif

        if (!amount)
            amount := 1;
        endif

        // print( "objtype=" + Hex(objtype) + ", amount=" + amount );
        
        local i;
        for( i := 1; i <= amount; i := i + 1 )
            SendSysMessage( who, "Now select a location on the ground, where you want the item to appear." );
            local place := TargetCoordinates( who );
            local item := CreateItemAtLocation( place.x, place.y, place.z, objtype, 1 );
            item.decayat := 0; 
            SetObjProperty(item, "gmmade", who.name + " // " + who.acctname );
        endfor
    else
        local selection := SelectMenuItem2( who, "ItemCreation" );
        if (selection)
            SendSysMessage( who, "Now select a location on the ground, where you want the item to appear." );
            local place := TargetCoordinates( who );
            local created := CreateItemAtLocation( place.x, place.y, place.z, selection.objtype, 1 );
            created.color := selection.color;
            created.decayat := 0;    
            SetObjProperty(created, "gmmade", who.name + " // " + who.acctname );
        endif
    endif
endprogram
