use uo;
use os;
use util;

include "include/client";
include "include/spellrestrictions";
include "include/spellattack";

program cast_fireball( parms )

	local circle := 3;
	local caster;
	local cast_on;

	if (parms[1] = "#MOB")
		caster := parms[2];
		cast_on := parms[3];
		send_attack(cast_on,caster, 18);
	else
		caster := parms;
		cast_on := CanTargetSpell(caster, circle, TGTOPT_HARMFUL + TGTOPT_CHECK_LOS );
		if (!cast_on)
			return;
		endif
	endif

	SetObjProperty(cast_on, "LastHit", {caster.name, caster.serial, caster });
	PlayMovingEffect( caster, cast_on, FX_FIREBALL, 5, 1 );
	PlaySoundEffect( caster, SFX_SPELL_FIREBALL );

	local dmg := CalcSpellDamage(circle,caster,cast_on);

	if (Reflected(cast_on))
		PlayMovingEffect( cast_on, caster, FX_FIREBALL, 5, 1 );
		PlaySoundEffect( cast_on, SFX_SPELL_FIREBALL );
		cast_on := caster;
		if (GetObjProperty( caster, "mr") = "1")
			EraseObjProperty(caster, "mr");
			SendSysMessage(caster, "Your magic reflect spell saved you!");
			return;
		endif
	endif
		    
	ApplyRawDamage( cast_on , Resisted(circle,caster,cast_on,dmg) );

endprogram

