use uo;
use util;
use os;
include "include/client";
include "include/spellrestrictions";
include "include/spellattack";

program cast_lightning( parms )

	local circle := 4;
	local caster;
	local cast_on;

	if (parms[1] = "#MOB")
		caster := parms[2];
		cast_on := parms[3];
		send_attack(cast_on,caster, 30);
	else
		caster := parms;
		cast_on := CanTargetSpell(caster, circle, TGTOPT_HARMFUL + TGTOPT_CHECK_LOS );
		if (!cast_on)
			return;
		endif
	endif

	local dmg := CalcSpellDamage(circle,caster,cast_on);

	if (Reflected(cast_on))
		cast_on := caster;
		if (GetObjProperty( caster, "mr") = "1")
			EraseObjProperty(caster, "mr");
			SendSysMessage(caster, "Your magic reflect spell saved you!");
			return;
		endif
	endif

	SetObjProperty(cast_on, "LastHit", {caster.name, caster.serial, caster });
	PlaySoundEffect(cast_on, SFX_SPELL_LIGHTNING);
	PlayLightningBoltEffect( cast_on);
	ApplyRawDamage( cast_on , Resisted(circle,caster,cast_on,dmg) );

endprogram
